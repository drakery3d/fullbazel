load("@npm//@bazel/rollup:index.bzl", "rollup_bundle")
load("@npm//@bazel/terser:index.bzl", "terser_minified")
load("@npm//history-server:index.bzl", "history_server")
load("@npm//@bazel/typescript:index.bzl", "ts_devserver", "ts_library")
load("@io_bazel_rules_sass//:defs.bzl", "sass_binary")
load("@build_bazel_rules_nodejs//:index.bzl", "nodejs_binary", "pkg_web")
load("@io_bazel_rules_docker//nodejs:image.bzl", "nodejs_image")
load("//tools:angular_ts_library.bzl", "ng_ts_library")
load("//tools:insert_html_assets.bzl", "insert_html_assets")
load("//tools:pkg_pwa.bzl", "pkg_pwa")

package(default_visibility = ["//visibility:public"])

exports_files([
    "ngsw-config.json",
    "tsconfig.json",
])

sass_binary(
    name = "styles",
    src = "styles.sass",
)

ts_library(
    name = "initialize_testbed",
    testonly = True,
    srcs = ["initialize_testbed.ts"],
    deps = [
        "@npm//@angular/core",
        "@npm//@angular/platform-browser-dynamic",
    ],
)

ng_ts_library(
    name = "dev_src",
    srcs = ["main.dev.ts"],
    tsconfig = "//client:tsconfig.json",
    deps = [
        "//client/app:dev",
        "@npm//@angular/platform-browser",
    ],
)

insert_html_assets(
    name = "inject_scripts_for_dev",
    outs = ["dev/index.html"],
    asset_paths = [
        "$(execpath :styles.css)",
        "$(execpath @npm//:node_modules/zone.js/dist/zone.min.js)",
        "./_/ts_scripts.js",
    ],
    data = [
        ":app.html",
        ":styles.css",
        "@npm//:node_modules/zone.js/dist/zone.min.js",
    ],
    html_file = ":app.html",
)

ts_devserver(
    name = "dev_server",
    additional_root_paths = ["client/dev"],
    entry_module = "abs/client/main.dev",
    port = 4200,
    scripts = [
        "@npm//:node_modules/tslib/tslib.js",
        "//tools:rxjs_umd_modules",
        "@npm//socket.io-client:socket.io-client__umd",
    ],
    static_files = [
        "@npm//:node_modules/zone.js/dist/zone.min.js",
        "//client/assets",
        ":inject_scripts_for_dev",
        ":styles.css",
        ":favicon.ico",
    ],
    deps = ["//client:dev_src"],
)

ng_ts_library(
    name = "prod_src",
    srcs = ["main.prod.ts"],
    tsconfig = "//client:tsconfig.json",
    deps = [
        "//client/app:prod",
        "@npm//@angular/core",
        "@npm//@angular/platform-browser",
    ],
)

rollup_bundle(
    name = "bundle",
    config_file = "//tools:rollup.config.js",
    entry_points = {
        ":main.prod.ts": "index.es2015",
    },
    output_dir = True,
    deps = [
        ":prod_src",
        "@npm//rollup-plugin-commonjs",
        "@npm//rollup-plugin-node-resolve",
    ],
)

terser_minified(
    name = "bundle.min",
    src = ":bundle",
    config_file = "//tools:terser.config.json",
)

insert_html_assets(
    name = "asset_injected_index_html",
    outs = ["index.html"],
    asset_paths = [
        "$(execpath styles.css)",
        "$(execpath @npm//:node_modules/zone.js/dist/zone.min.js)",
        "$(execpath bundle.min)/index.es2015.js",
    ],
    data = [
        ":app.html",
        ":bundle.min",
        ":styles.css",
        "@npm//:node_modules/zone.js/dist/zone.min.js",
    ],
    html_file = ":app.html",
)

pkg_web(
    name = "web_app",
    srcs = [
        "app.html",
        ":asset_injected_index_html",
        ":bundle.min",
        ":favicon.ico",
        ":robots.txt",
        ":styles.css",
        "//client/assets",
        "@npm//:node_modules/zone.js/dist/zone.min.js",
    ],
)

history_server(
    name = "app_server",
    data = [":web_app"],
    templated_args = ["-a $$(rlocation $(rootpath :web_app))"],
)

insert_html_assets(
    name = "asset_injected_pwa_index_html",
    outs = ["_/index.html"],
    asset_paths = [
        "$(execpath styles.css)",
        "$(execpath @npm//:node_modules/zone.js/dist/zone.min.js)",
        "$(execpath bundle.min)/index.es2015.js",
    ],
    data = [
        ":bundle.min",
        ":pwa.html",
        ":styles.css",
        "@npm//:node_modules/zone.js/dist/zone.min.js",
    ],
    html_file = ":pwa.html",
)

pkg_pwa(
    name = "web_pwa",
    srcs = [
        ":asset_injected_pwa_index_html",
        ":bundle.min",
        ":favicon.ico",
        ":manifest.webmanifest",
        ":robots.txt",
        ":styles.css",
        "//client/assets",
    ],
    index_html = ":asset_injected_pwa_index_html",
    ngsw_config = ":ngsw-config.json",
)

history_server(
    name = "pwa_server",
    data = [":web_pwa"],
    templated_args = ["-a $$(rlocation $(rootpath :web_pwa))"],
)

ts_library(
    name = "server_lib",
    srcs = ["server.ts"],
    deps = [
        "//client/app:server",
        "@npm//@angular/core",
        "@npm//@nguniversal/express-engine",
        "@npm//@types/express",
        "@npm//@types/node",
        "@npm//express",
    ],
)

nodejs_binary(
    name = "client",
    data = [
        ":server_lib",
        ":web_pwa",
    ],
    entry_point = ":server.ts",
)

nodejs_image(
    name = "prod_server_image",
    data = [
        ":server_lib",
        ":web_pwa",
    ],
    entry_point = ":server.ts",
)
