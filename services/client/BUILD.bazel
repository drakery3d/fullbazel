load("@npm//@bazel/rollup:index.bzl", "rollup_bundle")
load("@npm//@bazel/terser:index.bzl", "terser_minified")
load("@npm//history-server:index.bzl", "history_server")
load("@npm//@bazel/typescript:index.bzl", "ts_library")
load("@npm//@bazel/concatjs:index.bzl", "concatjs_devserver")
load("@io_bazel_rules_sass//:defs.bzl", "sass_binary")
load("@build_bazel_rules_nodejs//:index.bzl", "nodejs_binary", "pkg_web")
load("@io_bazel_rules_docker//nodejs:image.bzl", "nodejs_image")
load("@k8s_deploy//:defaults.bzl", "k8s_deploy")
load("@npm//@babel/cli:index.bzl", "babel")
load("//tools:angular_ts_library.bzl", "ng_ts_library")
load("//tools:insert_html_assets.bzl", "insert_html_assets")
load("//tools:pkg_pwa.bzl", "pkg_pwa")

package(default_visibility = ["//visibility:public"])

exports_files([
    "ngsw-config.json",
    "tsconfig.json",
])

sass_binary(
    name = "styles",
    src = "styles.sass",
)

ts_library(
    name = "initialize_testbed",
    testonly = True,
    srcs = ["initialize_testbed.ts"],
    tsconfig = "tsconfig.json",
    deps = [
        "@npm//@angular/core",
        "@npm//@angular/platform-browser-dynamic",
    ],
)

ng_ts_library(
    name = "dev_src",
    srcs = ["main.dev.ts"],
    tsconfig = "//services/client:tsconfig.json",
    deps = [
        "//services/client/app:dev",
        "@npm//@angular/platform-browser",
    ],
)

insert_html_assets(
    name = "inject_scripts_for_dev",
    outs = ["dev/index.html"],
    asset_paths = [
        "$(execpath :styles.css)",
        "$(execpath @npm//:node_modules/zone.js/dist/zone.min.js)",
        "./_/ts_scripts.js",
    ],
    data = [
        ":main.html",
        ":styles.css",
        "@npm//:node_modules/zone.js/dist/zone.min.js",
    ],
    html_file = ":main.html",
)

concatjs_devserver(
    name = "dev_server",
    additional_root_paths = ["services/client/dev"],
    entry_module = "abs/services/client/main.dev",
    port = 4200,
    scripts = [
        "@npm//:node_modules/tslib/tslib.js",
        "//tools:rxjs_umd_modules",
        "@npm//socket.io-client:socket.io-client__umd",
    ],
    static_files = [
        "@npm//:node_modules/zone.js/dist/zone.min.js",
        "//services/client/assets",
        ":inject_scripts_for_dev",
        ":styles.css",
        ":favicon.ico",
    ],
    deps = ["//services/client:dev_src"],
)

ng_ts_library(
    name = "prod_src",
    srcs = ["main.prod.ts"],
    tsconfig = "//services/client:tsconfig.json",
    deps = [
        "//services/client/app:prod",
        "@npm//@angular/core",
        "@npm//@angular/platform-browser",
    ],
)

rollup_bundle(
    name = "bundle-es2015",
    config_file = "//tools:rollup.config.js",
    entry_points = {
        ":main.prod.ts": "index.es2015",
    },
    output_dir = True,
    deps = [
        ":prod_src",
        "@npm//rollup-plugin-commonjs",
        "@npm//rollup-plugin-node-resolve",
    ],
)

babel(
    name = "bundle-es5",
    args = [
        "$(execpath :bundle-es2015)",
        "--no-babelrc",
        "--source-maps",
        "--presets=@babel/preset-env",
        "--out-dir",
        "$(@D)",
    ],
    data = [
        ":bundle-es2015",
        "@npm//@babel/preset-env",
    ],
    output_dir = True,
)

terser_minified(
    name = "bundle-es2015.min",
    src = ":bundle-es2015",
    config_file = "//tools:terser.config.json",
)

terser_minified(
    name = "bundle-es5.min",
    src = ":bundle-es5",
    config_file = "//tools:terser.config.json",
)

insert_html_assets(
    name = "asset_injected_index_html",
    outs = ["index.html"],
    asset_paths = [
        "$(execpath styles.css)",
        "$(execpath @npm//:node_modules/zone.js/dist/zone.min.js)",
        "$(execpath bundle-es2015.min)/index.es2015.js",
        "$(execpath bundle-es5.min)/index.es2015.js",
    ],
    data = [
        ":bundle-es2015.min",
        ":bundle-es5.min",
        ":main.html",
        ":styles.css",
        "@npm//:node_modules/zone.js/dist/zone.min.js",
    ],
    html_file = ":main.html",
)

pkg_web(
    name = "web_app",
    srcs = [
        "main.html",
        ":asset_injected_index_html",
        ":bundle-es2015.min",
        ":bundle-es5.min",
        ":favicon.ico",
        ":robots.txt",
        ":styles.css",
        "//services/client/assets",
        "@npm//:node_modules/zone.js/dist/zone.min.js",
    ],
)

history_server(
    name = "app_server",
    data = [":web_app"],
    templated_args = ["-a $$(rlocation $(rootpath :web_app))"],
)

insert_html_assets(
    name = "asset_injected_pwa_index_html",
    outs = ["_/index.html"],
    asset_paths = [
        "$(execpath styles.css)",
        "$(execpath @npm//:node_modules/zone.js/dist/zone.min.js)",
        "$(execpath bundle-es2015.min)/index.es2015.js",
        "$(execpath bundle-es5.min)/index.es2015.js",
    ],
    data = [
        ":bundle-es2015.min",
        ":bundle-es5.min",
        ":main.html",
        ":styles.css",
        "@npm//:node_modules/zone.js/dist/zone.min.js",
    ],
    html_file = ":main.html",
)

pkg_pwa(
    name = "web_pwa",
    srcs = [
        ":asset_injected_pwa_index_html",
        ":bundle-es2015.min",
        ":favicon.ico",
        ":manifest.webmanifest",
        ":robots.txt",
        ":styles.css",
        "//services/client/assets",
    ],
    index_html = ":asset_injected_pwa_index_html",
    ngsw_config = ":ngsw-config.json",
)

history_server(
    name = "pwa_server",
    data = [":web_pwa"],
    templated_args = ["-a $$(rlocation $(rootpath :web_pwa))"],
)

ts_library(
    name = "server_lib",
    srcs = ["server.ts"],
    tsconfig = "tsconfig.json",
    deps = [
        "//services/client/app:server",
        "@npm//@angular/core",
        "@npm//@nguniversal/express-engine",
        "@npm//@types/express",
        "@npm//@types/node",
        "@npm//express",
    ],
)

nodejs_binary(
    name = "client",
    data = [
        ":server_lib",
        ":web_pwa",
    ],
    entry_point = ":server.ts",
)

nodejs_image(
    name = "prod_server_image",
    data = [
        ":server_lib",
        ":web_pwa",
    ],
    entry_point = ":server.ts",
)

k8s_deploy(
    name = "kubernetes",
    images = {"client:placeholder_name": ":prod_server_image"},
    template = ":client.yaml",
)
